#!/usr/bin/env perl
###All rights reserved
###Author: lyk
###Updated 2017/10/21
#
use warnings;
use List::Util qw(first max maxstr min minstr reduce shuffle sum);
use Math::Round;
use Statistics::R;


print scalar localtime,"\n";
#######Default argument value;
my $SJOverhangMin = 3;
my $readCountMin  = 10;
my $mapq          = 30;
my $libType = ("fr-unstranded");
my $list_path = 'sample_list';
my $gtf_path  = 'top_gene.gtf';
my $out_dir = '';

##########Initial the global value;
my %flag1 = ("+" => {"fr-secondstrand" => 99,"fr-firststrand" => 83,"fr-unstranded" => 0},
		  "-" => {"fr-secondstrand" => 83,"fr-firststrand" => 99,"fr-unstranded" => 0});
my %flag2 = ("+" => {"fr-secondstrand" => 147 ,"fr-firststrand" => 163,"fr-unstranded" => 0},
		  "-" => {"fr-secondstrand" => 163,"fr-firststrand" => 147,"fr-unstranded" => 0});
#####SET the parameter
&setArgv;

my $out_merge_path = $out_dir.'Merge_transcripts.gtf';
my $out_splice_path = $out_dir.'ASEvent.tsv';
my $out_junc_path   = $out_dir.'Junction.tsv';



#######R Commands
my $cmds = <<EOF;
alle=matrix(x,nrow=2)
pvalue=chisq.test(alle)\$p.value
EOF
my $qval_cmds = <<EOF;
library(qvalue)
table <- read.csv("$out_splice_path",header=TRUE,sep="\t",check.names=FALSE)
qobj <- qvalue(p = table\$pvalue)
qvalue <- qobj\$qvalues
table\$qvalue<-as.numeric(qvalue)
write.table(table,"$out_splice_path",quote = FALSE,row.names=FALSE,col.names=TRUE,sep="\t", qmethod = "escape")
EOF
######End R Commands
# Create a communication bridge with R and start R
my $R = Statistics::R->new();



######Set the global argument value;
#######Read the sample information
print "Normalizing Sequence Deep...\n";
open SAMPLE,$list_path or die "Failed to open sample list file $list_path: No such file or directory. Exiting";
my @readCount;
my $total = 0;
my $i=0;
my @samCond;
my @sample;
my %repNum;
while(<SAMPLE>){
	chomp;
	my ($sample,$condition,$deep) = split;
	$readCount[$i] = $deep;
	$total += $deep; 
	$samCond[$i] = $condition;
	$repNum{$condition}++;
	$sample[$i] = $sample;
	$i++;
}

my %hash;
my @condition = grep { ++$hash{$_}<2 } @samCond;
my $average = $total/@readCount;
my @size_factor;
for(my $i=0;$i<@readCount;$i++){
	$size_factor[$i] = $average/$readCount[$i];
}
print "Sample: @sample\n";
print "sizeFactor: @size_factor\n";

open SPL,'>', $out_junc_path or die "Failed to open junction file $out_junc_path: No such file or directory. Exiting";
print SPL "Chrmosome\tStrand\tJunction\t",join( "\t",@sample),"\n";

#########I/O File
open FILE, "awk '(\$1 !~ /^\#|\_/ && \$2 != \"havana\"){print}' $gtf_path  |" or die "Failed to open gtf file $gtf_path: No such file or directory. Exiting";
open MERGE, '>',$out_merge_path or die "Failed to open merge transcripts file $out_merge_path: No such file or directory. Exiting";
open OUT,'>',$out_splice_path or die "Failed to alternative splicing file $out_splice_path: No such file or directory. Exiting";
print OUT "ase_type\tase_id\tgene_id\tgene_name\tchromosome\tstrand\tfixSplicingSite\tpercentDiff\tpvalue\tqvalue\talterSplicingSite, readCount($condition[0]|$condition[1]), percentage($condition[0]|$condition[1])\n";



my %gene;
print scalar localtime,"\n";
print "Merge transcripts...\n";
######Read the gene information
while(<FILE>){
	chomp;
	my ($chr,$source,$feature,$str,$end,$score,$strand,$frame,$attribute) = split /\t/;
	my %attr;
	foreach (split /;/,$attribute){
		my ($key,$val) = split;
		$val =~ s/\;|\"//g;
		$attr{$key} = $val;
	}
	if($feature eq "exon"){
		if(exists $gene{$attr{'gene_id'}}){
			push @{$gene{$attr{'gene_id'}}{'merge'}}, ($str..$end);
		}
		else{
			%{$gene{$attr{'gene_id'}}} = ('chr'=>$chr,'source'=>$source,'merge'=>[$str..$end],'strand'=>$strand,'gene_name'=>$attr{'gene_name'});
		}
	}
}
my @gene = keys %gene;

print scalar localtime,"\n";
print "ASE Calculating...\n";
my ($ID,$gene_tmp,$id_tmp,$type_tmp) = (0,0,0,0);
for my $i (0..$#gene){
	my $gene_id = $gene[$i];
	my $val = $gene{$gene_id};
	my @merge = @{$val->{'merge'}};
	my %hash;
	@merge = grep { ++$hash{$_} < 2 } @merge;
	my ($chr,$source,$strand,$gene_name) = ($val->{'chr'},$val->{'source'},$val->{'strand'},$val->{'gene_name'});
	if ($chr !~ /^chr/){
		$chr = "chr$chr";
		if ($chr eq "chrMT"){
			$chr = "chrM";
		}
	}

	my ($junc_ref,$anno_ref,$dpsi_ref,$pval_ref);
	if($strand eq "+"){
		@merge = sort{$a<=>$b} @merge;
		my $exon_num = 1;
		my @exon5p;
		my @exon3p;
		push @exon5p,$merge[0];
		######Print the gene lines
		print MERGE "$chr\t$source\_MERGE\tgene\t$merge[0]\t$merge[-1]\t.\t$strand\t.\tgene_id \"$gene_id\"; gene_name \"$gene_name\";\n";
		my $exonStr = $merge[0];
		for(my $i=1;$i<@merge;$i++){
			if($merge[$i]-$merge[$i-1]>1){
				push @exon3p, $merge[$i-1];
				push @exon5p, $merge[$i];
				print MERGE "$chr\t$source\_MERGE\texon\t$exonStr\t$merge[$i-1]\t.\t$strand\t.\tgene_id \"$gene_id\"; exon_number \"$exon_num\"; gene_name \"$gene_name\";\n";
				$exonStr = $merge[$i];
				$exon_num++;
			}
		}
		push @exon3p, $merge[-1];
		print MERGE "$chr\t$source\_MERGE\texon\t$exonStr\t$merge[-1]\t.\t$strand\t.\tgene_id \"$gene_id\"; exon_number \"$exon_num\"; gene_name \"$gene_name\";\n";
		($junc_ref) = &junction($chr,$strand,$merge[0],$merge[-1]);
		($anno_ref,$dpsi_ref,$pval_ref) = &annoExon($junc_ref,\@exon5p,\@exon3p,$chr,$strand);
	}

	else{
		@merge = sort{$b<=>$a} @merge;
		my $exon_num = 1;
		my @exon5p;
		my @exon3p;
		push @exon5p,$merge[0];
		######Print the gene lines
		print MERGE "$chr\t$source\_MERGE\tgene\t$merge[0]\t$merge[-1]\t.\t$strand\t.\tgene_id \"$gene_id\"; gene_name \"$gene_name\";\n";
		my $exonEnd = $merge[0];
		for(my $i=1;$i<@merge;$i++){
			if($merge[$i]-$merge[$i-1]<-1){
				push @exon3p, $merge[$i-1];
				push @exon5p, $merge[$i];
				print MERGE "$chr\t$source\_MERGE\texon\t$merge[$i-1]\t$exonEnd\t.\t$strand\t.\tgene_id \"$gene_id\"; exon_number \"$exon_num\"; gene_name \"$gene_name\";\n";
				$exonEnd = $merge[$i];
				$exon_num++;
			}
		}
		push @exon3p, $merge[-1];
		print MERGE "$chr\t$source\_MERGE\texon\t$merge[-1]\t$exonEnd\t.\t$strand\t.\tgene_id \"$gene_id\"; exon_number \"$exon_num\"; gene_name \"$gene_name\";\n";
		($junc_ref) = &junction($chr,$strand,$merge[-1],$merge[0]);
		($anno_ref,$dpsi_ref,$pval_ref) = &annoExon2($junc_ref,\@exon5p,\@exon3p,$chr,$strand);
	}
	foreach my $type (sort (keys %$anno_ref)){
		my ($ase_id,$ase_type,$junc1) = split /\t/,$type;
		if($type_tmp ne $ase_type || $id_tmp ne $ase_id || $gene_id ne $gene_tmp){$ID++};
		$type_tmp = $ase_type;
		$id_tmp = $ase_id;
		$gene_tmp = $gene_id;
		my $info = $anno_ref->{$type};
		printf OUT ("%s\t%s\t%s\t%s\t%s\t%s\t%s\t%.3f\t%s\t%s\t",$ID,$ase_type,$gene_id,$gene_name,$chr,$strand,$junc1,$dpsi_ref->{$junc1},$pval_ref->{$junc1},0);
		foreach my $k (sort (keys %$info)){
			my $v = $info->{$k};
			printf OUT ("%s,%d|%d,%.3f|%.3f;", $k,$v->{'count'}->{$condition[0]},$v->{'count'}->{$condition[1]},$v->{'percent'}->{$condition[0]},$v->{'percent'}->{$condition[1]});
		}
		print OUT "\n";
	}
}


$R->stop();
my $R2 = Statistics::R->new();
print scalar localtime,"\n";
print "Qvalue Calculating...\n";
$R2->run($qval_cmds);			####calculate the qvalue in R


sub annoExon2{
	my ($junc_ref,$exon5p,$exon3p,$chr,$strand) = @_;
	my %alter;   ####return value;
	my %sum;
	my %reten;
	while (my ($key,$val) = each %$junc_ref){
		my ($str5p,$end3p) = split /\-/,$key;
		my $max_index = $#{$exon5p};
		my ($exon_num3p,$exon_num5p) = ($max_index+1,1);
		my ($shift3p,$shift5p)       = ($end3p - $exon3p->[-1],$str5p-$exon5p->[0]);
		for my $i (0..$max_index){
			if($end3p > $exon5p->[$i]){
				$shift3p = ($i-1>=0) ? $end3p-$exon3p->[$i-1] : "";
				$exon_num3p = $i;
				last;
			}
		}

		for(my $i=$max_index;$i>=0;$i--){
			if($str5p < $exon3p->[$i]){
				$shift5p = ($i+1<=$max_index) ? $str5p-$exon5p->[$i+1] : "";
				$exon_num5p = ($i+1<=$max_index) ? $i+2 : -1;
				last;
			}
		}

		my $junc3c = "$end3p:exon$exon_num3p:$shift3p";
		my $junc5c = "$str5p:exon$exon_num5p:$shift5p";
		if($exon_num3p ne $exon_num5p){
			$alter{"5p\t$junc3c"}->{$junc5c}->{'count'} = $val;
			$alter{"3p\t$junc5c"}->{$junc3c}->{'count'} = $val;
			$sum{$junc3c}->{$condition[0]} += $val->{$condition[0]};
			$sum{$junc3c}->{$condition[1]} += $val->{$condition[1]};
			$sum{$junc5c}->{$condition[0]} += $val->{$condition[0]};
			$sum{$junc5c}->{$condition[1]} += $val->{$condition[1]};
		}
		else{
			$reten{$exon_num3p}->{"$junc5c,$junc3c"}->{'count'} = $val;
		}
	}
	my ($aseR) = &annoASE(\%alter);
	my ($alter_r,$deltapsi_r,$pvalue_r) = &stat($aseR,\%sum);
	my ($retention_r) = &retention(\%reten);
	($alter_r,$deltapsi_r,$pvalue_r) = &stat_retention($retention_r,$chr,$strand,$alter_r,$deltapsi_r,$pvalue_r);
	return ($alter_r,$deltapsi_r,$pvalue_r);
}


sub annoExon{
	my ($junc_ref,$exon5p,$exon3p,$chr,$strand) = @_;
	my %alter;   ####return value;
	my %sum;
	my %reten;
	while (my ($key,$val) = each %$junc_ref){
		my ($end3p,$str5p) = split /\-/,$key;
		my $max_index = $#{$exon5p};
		my ($exon_num3p,$exon_num5p) = ($max_index+1,1);
		my ($shift3p,$shift5p)       = ($end3p - $exon3p->[-1],$str5p-$exon5p->[0]);
		for my $i (0..$max_index){
			if($end3p < $exon5p->[$i]){
				$shift3p = ($i-1>=0) ? $end3p-$exon3p->[$i-1] : "";
				$exon_num3p = $i;
				last;
			}
		}

		for(my $i=$max_index;$i>=0;$i--){
			if($str5p > $exon3p->[$i]){
				$shift5p = ($i+1<=$max_index) ? $str5p-$exon5p->[$i+1] : "";
				$exon_num5p = ($i+1<=$max_index) ? $i+2 : -1;
				last;
			}
		}

		my $junc3c = "$end3p:exon$exon_num3p:$shift3p";
		my $junc5c = "$str5p:exon$exon_num5p:$shift5p";
		if($exon_num3p ne $exon_num5p){
			$alter{"5p\t$junc3c"}->{$junc5c}->{'count'} = $val;
			$alter{"3p\t$junc5c"}->{$junc3c}->{'count'} = $val;
			$sum{$junc3c}->{$condition[0]} += $val->{$condition[0]};
			$sum{$junc3c}->{$condition[1]} += $val->{$condition[1]};
			$sum{$junc5c}->{$condition[0]} += $val->{$condition[0]};
			$sum{$junc5c}->{$condition[1]} += $val->{$condition[1]};
		}
		else{
			$reten{$exon_num3p}->{"$junc3c,$junc5c"}->{'count'} = $val;
		}
	}
	my ($aseR) = &annoASE(\%alter);
	my ($alter_r,$deltapsi_r,$pvalue_r) = &stat($aseR,\%sum);
	my ($retention_r) = &retention(\%reten);
	($alter_r,$deltapsi_r,$pvalue_r) = &stat_retention($retention_r,$chr,$strand,$alter_r,$deltapsi_r,$pvalue_r);
	return ($alter_r,$deltapsi_r,$pvalue_r);
}

sub retention{
	my $reten = $_[0];
	my %retention;
	my $k=0;
	while(my ($exon,$val) = each %$reten){
		my %stat;
		my $j = 0;
		foreach my $key (sort keys %$val){
			my ($junc1,$junc2) = split /,/,$key;
			my ($str) = split /:/,$junc1;
			my ($end) = split /:/,$junc2;
			if($j == 0){
				$stat{$str} = $end;
			}
			else{
				my $i = 0;
				while(my ($st,$en) = each %stat){
					if($str<=$en && $end>=$st){
						$i++;
						if($end >$en){
							$stat{$st} = $end;
						}
						if($str < $st){
							$stat{$str} = $end;
							delete $stat{$st};
						}
					}
				}
				if($i == 0){
					$stat{$str} = $end;
				}
			}
			$j++;
		}
		while(my ($str,$end) = each %stat){
			while(my ($junc,$count) = each %$val){
				my ($junc1,$junc2) = split /,/,$junc;
				my ($str2) = split /:/,$junc1;
				my ($end2) = split /:/,$junc2;
				if($str<=$str2 && $end >= $end2){
					$retention{"$k\tRI\texon$exon:$str-$end"}->{"$junc1->$junc2"} = $count;
				}
			}
			$k++;
		}
	}
	return (\%retention);
}
sub annoASE{
	my ($alterR) = @_;
	my %ase;
	my @cse;
	my %exon3p;
	my %exon5p;
	my %eachASE;
	my $aseNum = 0;
	while(my($key,$val) = each %$alterR){
		my $junc_num = keys %$val;
		if($junc_num > 2){
			my ($link,$end) = split /\t/,$key;
			push @cse, (split /\:/,$end)[1];
			if ($link =~ /3p/){
				$ase{"0\tCSE\t$end"} = $val;
			}
			else{
				$ase{"0\tCSE\t$end"} = $val;
			}
			foreach my $junc (keys %$val){
				push @cse, (split /\:/,$junc)[1];
			}
		}
		elsif($junc_num == 2){
			my ($link,$end) = split /\t/,$key;
			$eachASE{$end} = $val;
			if($link =~ /3p/){
				push @{$exon3p{$end}},(split /\:/,$end)[1];
				foreach my $junc (keys %$val){
					push @{$exon3p{$end}}, (split /\:/,$junc)[1];
				}
			}
			else{
				push @{$exon5p{$end}},(split /\:/,$end)[1];
				foreach my $junc (keys %$val){
					push @{$exon5p{$end}}, (split /\:/,$junc)[1];
				}
			}
		}
	}
	while(my($key,$val) = each %exon3p){
		my $k =0;
		foreach my $exon (@$val){
			if(grep {$_ eq $exon} @cse){$k++;}
		}
		if($k >= 2){
			$ase{"0\tCSE\t$key"} = $eachASE{$key};
			delete $exon3p{$key};
		}
	}
	while(my($key,$val) = each %exon5p){
		my $k =0;
		foreach my $exon (@$val){
			if(grep {$_ eq $exon} @cse){$k++;}
		}
		if($k >= 2){
			$ase{"0\tCSE\t$key"} = $eachASE{$key};
			delete $exon5p{$key};
		}
	}

	while(my($key,$val) = each %exon3p){
		my $j =0;
		while(my($key2,$val2) = each %exon5p){
			my $i = 0;
			foreach my $exon (@$val2){
				if(grep {$_ eq $exon} @$val){$i++;}
			}
			if($i == 3){
				$aseNum++;
				$ase{"$aseNum\tSE\t$key"} = $eachASE{$key};
				$ase{"$aseNum\tSE\t$key2"} = $eachASE{$key2};
				delete $exon5p{$key2};
				$j++;
			}
			elsif($i == 2){
				$aseNum++;
				$ase{"$aseNum\tMXE\t$key"} = $eachASE{$key};
				$ase{"$aseNum\tMXE\t$key2"} = $eachASE{$key2};
				delete $exon5p{$key2};
				$j++;
			}
		}
		if($j == 0){
			my %hash;
			if(grep { ++$hash{$_} > 1 } @$val){
				$aseNum++;
				$ase{"$aseNum\tA3SS\t$key"} = $eachASE{$key};
			}
			else{
				$aseNum++;
				$ase{"$aseNum\tAFE\t$key"} = $eachASE{$key};
			}
		}
	}

	while(my($key,$val) = each %exon5p){
		my %hash;
		if(grep { ++$hash{$_} > 1 } @$val){
			$aseNum++;
			$ase{"$aseNum\tA5SS\t$key"} = $eachASE{$key};
		}
		else{
			$aseNum++;		
			$ase{"$aseNum\tALE\t$key"} = $eachASE{$key};
		}
	}
	return (\%ase);
}


sub stat{
	my ($aseR,$sumR) = @_;
	my %deltapsi;####return value;
	my %pvalue;	 ####return value;

	while (my ($type,$val) = each %$aseR){
		my ($key) = (split /\t/,$type)[2];
		my @count;
		if($sumR->{$key}->{$condition[0]} > 0 && $sumR->{$key}->{$condition[1]} > 0){
			while(my ($junc,$val2) = each %$val){
				#####Global variable @condition;
				my $count0 = $val2->{'count'}->{$condition[0]};
				my $count1 = $val2->{'count'}->{$condition[1]};
				#next if ($count0+$count1) < $readCountMin*2;
				push @count, $count0,$count1;
				my $psi0 = $count0/$sumR->{$key}->{$condition[0]};
				my $psi1 = $count1/$sumR->{$key}->{$condition[1]};
				$val2->{'percent'}->{$condition[0]} = $psi0;
				$val2->{'percent'}->{$condition[1]} = $psi1;
				$deltapsi{$key} += abs($psi0-$psi1);
			}

			$deltapsi{$key} /=2;    #####Set the largest value to 100%;

			$R->set( 'x', \@count );####set the value in R
			$R->run($cmds);			####calculate the pvalue in R
			$pvalue{$key} = $R->get('pvalue');	####get the pvalue from R
		}
		else{
			delete $aseR->{$type};
		}
	}
	return ($aseR,\%deltapsi,\%pvalue);
}


sub stat_retention{
	my ($retention_r,$chr,$strand,$alter_r,$deltapsi_r,$pvalue_r) = @_;
	while(my ($key,$val) = each %$retention_r){
		my @sum = (0)x@sample;
		my %reten_count = ($condition[0] => 0,$condition[1] => 0);
		my $reten_num = 0;
		my @count;
		my @total;
		while(my ($key2,$val2) = each %$val){
			$reten_num++;
			$total[0] += $val2->{'count'}->{$condition[0]};
			$total[1] += $val2->{'count'}->{$condition[1]};
			my @junc = (split /\:|\-\>/,$key2)[0,3];
			foreach my $junc (@junc){
				my $pos = $junc-$SJOverhangMin+1;    #####The same overhang
				for(my $i=0;$i<@sample;$i++){
					open BAM, "samtools view -q $mapq $sample[$i] $chr:$pos-$pos | awk '(\$6 !~ \"N\" && (\$2 != $flag1{$strand}->{$libType} && \$2 != $flag2{$strand}->{$libType}) && \$6 !~ \"S\" && \$6 !~ \"D\" && \$6 !~ \"I\"){print \$4,\$6}' | ";
					while(<BAM>){
						my ($str,$CIGAR) = split;
						my $readLen = substr($CIGAR,0,-1);
						if($str+$readLen-$junc >= $SJOverhangMin){
							$sum[$i]++;		
						}
					}
				}
			}
		}
		for(my $i=0;$i<@sum;$i++){
			$sum[$i] /= $reten_num*2/$size_factor[$i];
			$reten_count{$samCond[$i]} += $sum[$i];
		}

		my $j=0;
		foreach my $k (@condition){
			$reten_count{$k} /= $repNum{$k};
			$total[$j] += $reten_count{$k};
			push @count,$reten_count{$k};
			$j++;
		}

		if ($total[0] >0 && $total[1] >0){
			my ($id,$type,$exon) = split /\t/,$key;
			while(my($key2,$val2) = each %$val){
				my $count0 = $val2->{'count'}->{$condition[0]};
				my $count1 = $val2->{'count'}->{$condition[1]};
				push @count, $count0,$count1;
				my $psi0 = $count0/$total[0];
				my $psi1 = $count1/$total[1];
				$alter_r->{"$key\t$exon"}->{$key2}->{'count'}->{$condition[0]} = $count0;
				$alter_r->{"$key\t$exon"}->{$key2}->{'count'}->{$condition[1]} = $count1;
				$alter_r->{"$key\t$exon"}->{$key2}->{'percent'}->{$condition[0]} = $psi0;
				$alter_r->{"$key\t$exon"}->{$key2}->{'percent'}->{$condition[1]} = $psi1;
				$deltapsi_r->{$exon} += abs($psi0-$psi1);
			}
			$deltapsi_r->{$exon} /=2;    #####Set the largest value to 100%;

			$R->set( 'x', \@count );####set the value in R
			$R->run($cmds);			####calculate the pvalue in R
			$pvalue_r->{$exon} = $R->get('pvalue');	####get the pvalue from R
		}
	}
	return ($alter_r,$deltapsi_r,$pvalue_r);
}



sub junction{
	my ($chr,$strand,$str,$end) = @_;
	my %junc;
	my %ave_junc;  ###return value;

	for(my $i=0;$i<@sample;$i++){
		open BAM, "samtools view -q $mapq $sample[$i] $chr:$str-$end | awk '(\$6 ~ \"N\" && (\$2 != $flag1{$strand}->{$libType} && \$2 != $flag2{$strand}->{$libType}) && \$6 !~ \"S\" && \$6 !~ \"D\" && \$6 !~ \"I\"){print \$4,\$6}' | ";
		while(<BAM>){
			my ($str,$CIGAR) = split;
			my ($exon1,$exon2,$exon3,$str2,$str3,$end1,$end2,$end3);
			my @info = split /N|M/,$CIGAR;
			if(@info == 3){
				if($info[0] >=$SJOverhangMin && $info[2] >=$SJOverhangMin){
					$end1 = $str+$info[0]-1;  ####
					$str2 = $end1+$info[1]+1;
					$junc{"$end1-$str2"}->[$i]++;
				}
			}
			elsif(@info == 5){
				if($info[0] >=$SJOverhangMin && $info[2] >=$SJOverhangMin && $info[4] >=$SJOverhangMin){
					$end1 = $str+$info[0]-1;
					$str2 = $end1+$info[1]+1;
					$end2 = $str2+$info[2]-1;
					$str3 = $end2+$info[3]+1;
					$junc{"$end1-$str2"}->[$i]++;
					$junc{"$end2-$str3"}->[$i]++;
				}
			}

		}

	}

	foreach my $key (sort (keys %junc)){
		my %sum = ($condition[0]=>0,$condition[1]=>0);
		my %zeroCount = ($condition[0]=>0,$condition[1]=>0);
		my $total_sum = 0;
		for my $i (0..$#sample){
			if(!defined ($junc{$key}->[$i])){
				$junc{$key}->[$i] = 0;
				$zeroCount{$samCond[$i]}++;
			}
			else{
				$junc{$key}->[$i] *= $size_factor[$i];
			}
			$sum{$samCond[$i]} += $junc{$key}->[$i];
			$total_sum += $junc{$key}->[$i];
		}
		next if ($total_sum < $readCountMin*@sample || ($zeroCount{$condition[0]} > 0 && $zeroCount{$condition[1]} > 0 ));  ####Filtering
		while(my ($k,$v) = each %sum){
			$ave_junc{$key}->{$k} = round($v/$repNum{$k});
		}
		print SPL "$chr\t$strand\t$key";

		for my $i (0..$#sample){
			print SPL "\t$junc{$key}->[$i]";
		}
		print SPL "\n";
	}
	return (\%ave_junc);
}


sub setArgv{
	my %argv;
	for(my $i=0;$i<@ARGV;$i+=2){
		$argv{$ARGV[$i]} = $ARGV[$i+1]; 
	}
	while(my ($key,$val)=each %argv){
		if($key eq "--help"){
			printf ("%s\n\n%s\n  %s\n  %s\n  %s\n  %s\n  %s\n  %s\n  %s\n  %s\n","Usage: ASE.pl [options]","Options:","--help\tshow this help message and exit","--mapq\tDefult: $mapq. Minimum mapping quality (phred scaled) for an alignment to be considered as uniquely mapped","--libType\tDefault: $libType.  Use fr-firststrand or fr-secondstrand for strand-specific data.","--overhangMin\tDefult: $SJOverhangMin. Minimum overhang length for splice junctions on both sides. This value should be equal or greater than used in the aligner","--readCountMin\tDefult: $readCountMin. Minium average splice junctions read count to be considered","--sampleList\ta file listing sorted bam file paths of each sample and its conditon in tab-delimited format in each lines.","--gtf\tAn annotation of genes and transcripts in GTF format","--outDir\tThe output directory");
			exit ;
		}
		elsif($key eq "--libType"){
			if($val ne "fr-unstranded" && $val ne "fr-firststrand" && $val ne "fr-secondstrand"){
				die "Unrecognized libType $val.Default is unstranded (fr-unstranded). Use fr-firststrand or fr-secondstrand for strand-specific data. Exiting";
			}
			$libType = $val;
		}
		elsif($key eq "--outDir"){
			$out_dir = $val;
		}
		elsif($key eq "--gtf"){
			$gtf_path = $val;
		}
		elsif($key eq "--sampleList"){
			$list_path = $val;
		}
		elsif($key eq "--overhangMin"){
			die "$val if not an integer for argument --overhangMin. Exiting" if $val !~ /^\d+$/;
			$SJOverhangMin = $val;
		}
		elsif($key eq "--readCountMin"){
			die "$val if not an integer for argument --readCountMin. Exiting" if $val !~ /^\d+$/;
			$readCountMin = $val;
		}
		elsif($key eq "--mapq"){
			die "$val if not an integer for argument --mapq. Exiting" if $val !~ /^\d+$/;
			$readCountMin = $val;
		}
		else{
			die "Unrecognized argument $key. Exiting";
		}
	}
}
